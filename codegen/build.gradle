plugins {
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

apply plugin: 'java-library'

dependencies {
    swaggerCodegen "io.swagger.codegen.v3:swagger-codegen-cli:3.0.13"
}

swaggerSources {
    def basePackage = 'com.example.petstore'

    shared {
        inputFile = file("${rootDir}/contracts/shared/dummy_contract.yaml")
        code {
            language = 'java'
            rawOptions = [
                    "--group-id=${basePackage}",
                    "--model-package=${basePackage}.shared.models",
            ]
            components = [
                    models         : ['Error', 'Pet'],
                    modelDocs      : false,
                    modelTests     : false,
                    apis           : false,
                    apiDocs        : false,
                    apiTests       : false,
                    supportingFiles: false
            ]
        }
    }

    petstore {
        inputFile = file("${rootDir}/contracts/endpoints/petstore.yaml")
        code {
            language = 'java'
            configFile = file("${projectDir}/config/java.json")
            rawOptions = [
                    "--group-id=${basePackage}",
                    "--api-package=${basePackage}.store.api",
                    "--model-package=${basePackage}.store.models",
                    "--invoker-package=${basePackage}.store",
                    "--import-mappings=Pet=${basePackage}.shared.models.Pet,Error=${basePackage}.shared.models.Error"
            ]
            components = [
                    models         : true,
                    modelDocs      : false,
                    modelTests     : false,
                    apis           : true,
                    apiDocs        : false,
                    apiTests       : false,
                    supportingFiles: true
            ]
        }
    }

    pets {
        inputFile = file("${rootDir}/contracts/endpoints/pets.yaml")
        code {
            language = 'java'
            configFile = file("${projectDir}/config/java.json")
            rawOptions = [
                    "--group-id=${basePackage}",
                    "--api-package=${basePackage}.pets.api",
                    "--model-package=${basePackage}.pets.models",
                    "--invoker-package=${basePackage}.pets",
                    "--import-mappings=Pet=${basePackage}.shared.models.Pet,Error=${basePackage}.shared.models.Error"
            ]
            components = [
                    models         : true,
                    modelDocs      : false,
                    modelTests     : false,
                    apis           : true,
                    apiDocs        : false,
                    apiTests       : false,
                    supportingFiles: true
            ]
        }
    }
}

[swaggerSources.shared, swaggerSources.petstore, swaggerSources.pets].each {
    compileJava.dependsOn(it.code)
    sourceSets.main.java.srcDirs("${it.code.outputDir}/src/main/java")
}